grammar org.eclipse.scout.saml.Saml  with org.eclipse.xtext.xbase.Xbase

generate saml "http://www.eclipse.org/scout/saml/Saml"

import "http://www.eclipse.org/xtext/common/JavaVMTypes" as types
import "http://www.eclipse.org/xtext/xbase/Xbase" as xbase

Model:
	module=ModuleElement
	(imports+=ImportElement)*
	(
		(translations+=TranslationElement)
		|
		(codes+=CodeElement)
		|
		(lookups+=LookupElement)
		|
		(templates+=TemplateElement)
		|
		(forms+=FormElement)
	)*;


// TYPES
QualifiedNameWithWildCard: QualifiedName  ('.' '*')?;
BooleanType: 'true'|'false';	//TODO: code completion
LogicEventType: 'all'|'modify_load'|'modify_store'|'new_load'|'new_store'|'changed'|'click'|'master_changed'|'init'|'format_value';	//TODO: code completion depending on element we are on


ModuleElement: 'module' name=QualifiedName;


ImportElement: 'import' importedNamespace=QualifiedNameWithWildCard;


TranslationElement:'translation' name=ID (translations+=LanguageAttribute)+;
LanguageAttribute:lang=ID '=' text=STRING;


//TODO: code completion for placement, event
LogicElement: {LogicElement} 'logic' (name=ID)? (('event' '=' event=LogicEventType)? & ('placement' '=' placement=('client'|'server'|'inline'))? & ('exec' '=' exec=[LogicElement])?) ('{'source=STRING/*XBlockExpression*/'}')?;


CodeElement:'code' name=ID 'id' '=' id=INT;


LookupElement:'lookup' name=ID '{' logic+=LogicElement* '}';


TemplateElement:'template' name=ID 'definition' '=' definition=STRING/*JvmTypeReference*/;



FormElement:'form' name=ID (('modal' '=' modal=BooleanType)? & ('columns' '=' columns=INT)? & ('title' '=' title=[TranslationElement])? & ('subtitle' '=' subtitle=[TranslationElement])?)
	('{' (logic+=LogicElement | fields+=FormFieldElement)* '}')?;


// UI FIELDS

FormFieldElement:CompositeFieldElement | ValueFieldElement | CustomFieldElement | ButtonElement | TableElement;

// COMPOSITES
CompositeFieldElement:SequenceBoxElement|GroupBoxElement;
SequenceBoxElement:'sequence_box' name=ID
	(
		('text' '=' text=[TranslationElement])? & 
		('enabled' '=' enabled=BooleanType)? &
		('visible' '=' visible=BooleanType)? &
		('label_visible' '=' labelVisible=BooleanType)? &
		('master' '=' master=[ValueFieldElement])? &
		('grid_height' '=' gridHeight=INT)? &
		('grid_width' '=' gridWidth=INT)?
	)
	('{' (logic+=LogicElement | fields+=FormFieldElement)* '}')?;
	
GroupBoxElement:'group_box' name=ID
	(
		('text' '=' text=[TranslationElement])? &
		('columns' '=' columns=INT)? &
		('enabled' '=' enabled=BooleanType)? &
		('visible' '=' visible=BooleanType)? &
		('label_visible' '=' labelVisible=BooleanType)? &
		('border_visible' '=' borderVisible=BooleanType)? &
		('border_decoration' '=' borderDecoration=('empty'|'line'|'section'|'auto'))
		('master' '=' master=[ValueFieldElement])? &
		('grid_height' '=' gridHeight=INT)? &
		('grid_width' '=' gridWidth=INT)?
	)
	('{' (logic+=LogicElement | fields+=FormFieldElement)* '}')?;


// VALUE FIELDS
ValueFieldElement: StringElement|DoubleElement|LongElement|DateElement|SmartfieldElement;

StringElement:'string' name=ID 
	(
		('text' '=' text=[TranslationElement])? &
		('enabled' '=' enabled=BooleanType)? &
		('visible' '=' visible=BooleanType)? &
		('label_visible' '=' labelVisible=BooleanType)? &
		('master' '=' master=[ValueFieldElement])? &
		('mandatory' '=' mandatory=BooleanType)? &
		('maxlen' '=' maxlen=INT)? &
		('grid_height' '=' gridHeight=INT)? &
		('grid_width' '=' gridWidth=INT)?
	)
	('{' logic+=LogicElement* '}')?;


DoubleElement:'double' name=ID 
	(
		('text' '=' text=[TranslationElement])? &
		('enabled' '=' enabled=BooleanType)? &
		('visible' '=' visible=BooleanType)? &
		('label_visible' '=' labelVisible=BooleanType)? &
		('master' '=' master=[ValueFieldElement])? &
		('mandatory' '=' mandatory=BooleanType)? &
		('grid_height' '=' gridHeight=INT)? &
		('grid_width' '=' gridWidth=INT)?
	)
	('{' logic+=LogicElement* '}')?;

LongElement:'long' name=ID
	(
		('text' '=' text=[TranslationElement])? &
		('enabled' '=' enabled=BooleanType)? &
		('visible' '=' visible=BooleanType)? &
		('label_visible' '=' labelVisible=BooleanType)? &
		('master' '=' master=[ValueFieldElement])? &
		('mandatory' '=' mandatory=BooleanType)? &
		('grid_height' '=' gridHeight=INT)? &
		('grid_width' '=' gridWidth=INT)?
	)
	('{' logic+=LogicElement* '}')?;

DateElement:'date' name=ID
	(
		('text' '=' text=[TranslationElement])? &
		('enabled' '=' enabled=BooleanType)? &
		('visible' '=' visible=BooleanType)? &
		('label_visible' '=' labelVisible=BooleanType)? &
		('master' '=' master=[ValueFieldElement])? &
		('mandatory' '=' mandatory=BooleanType)? &
		('grid_height' '=' gridHeight=INT)? &
		('grid_width' '=' gridWidth=INT)?
	)
	('{' logic+=LogicElement* '}')?;

SmartfieldElement:'smartfield' name=ID
	(
		('text' '=' text=[TranslationElement])? &
		('enabled' '=' enabled=BooleanType)? &
		('visible' '=' visible=BooleanType)? &
		('label_visible' '=' labelVisible=BooleanType)? &
		('master' '=' master=[ValueFieldElement])? &
		('mandatory' '=' mandatory=BooleanType)? &
		('code' '=' code=[CodeElement])? &
		('value_type' '=' valueType=STRING/*JvmTypeReference*/)? &
		('lookup' '=' lookup=[LookupElement])? &
		('grid_height' '=' gridHeight=INT)? &
		('grid_width' '=' gridWidth=INT)?
	)
	('{' (logic+=LogicElement | menus+=MenuElement)* '}')?;

// CUSTOM FIELDS
CustomFieldElement:'custom_field' name=ID 
	(
		('template' '=' template=[TemplateElement]) &
		('text' '=' text=[TranslationElement])? &
		('enabled' '=' enabled=BooleanType)? &
		('visible' '=' visible=BooleanType)? &
		('label_visible' '=' labelVisible=BooleanType)? &
		('master' '=' master=[ValueFieldElement])? &
		('grid_height' '=' gridHeight=INT)? &
		('grid_width' '=' gridWidth=INT)?
	)
	('{' (logic+=LogicElement | fields+=FormFieldElement)* '}')?;


// BUTTONS
ButtonElement:'button' name=ID
	(
		('text' '=' text=[TranslationElement])? &
		('enabled' '=' enabled=BooleanType)? &
		('visible' '=' visible=BooleanType)? &
		('label_visible' '=' labelVisible=BooleanType)? &
		('master' '=' master=[ValueFieldElement])? &
		('process_button' '=' processButton=BooleanType)? &
		('grid_height' '=' gridHeight=INT)? &
		('grid_width' '=' gridWidth=INT)?
	)
	('{' (logic+=LogicElement | menus+=MenuElement)* '}')?;


// MENUS
MenuElement:'menu' name=ID
	(
		('enabled' '=' enabled=BooleanType)? &
		('visible' '=' visible=BooleanType)? &
		('text' '=' text=[TranslationElement])?
	)
	('{' logic+=LogicElement* '}')?;

TableElement:'table' name=ID
	(
		('text' '=' text=[TranslationElement])? &
		('enabled' '=' enabled=BooleanType)? &
		('visible' '=' visible=BooleanType)? &
		('master' '=' master=[ValueFieldElement])? &
		('label_visible' '=' labelVisible=BooleanType)? &
		('grid_height' '=' gridHeight=INT)? &
		('grid_width' '=' gridWidth=INT)?
	)
	('{' (logic+=LogicElement | menus+=MenuElement | columns+=ColumnElement)* '}')?;

	
ColumnElement:'column' name=ID
	(
		('type' '=' type=('date'|'string'|'int'|'double')) &
		('text' '=' text=[TranslationElement])? &
		('width' '=' width=INT)? &
		('visible' '=' visible=BooleanType)?
	);